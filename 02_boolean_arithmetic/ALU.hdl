// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],      
        zx,
        nx,
        zy,
        ny,
        f,
        no;
    OUT 
        out[16],
        zr,
        ng;
        
    PARTS:
    
    // handling the x part
    Mux16(a=x , b=false , sel=zx , out=xmux );
    Not16(in=xmux , out=notxmux );
    Mux16(a=xmux , b=notxmux , sel=nx , out=xmux2 );
   
   // handling the y part
    Mux16(a=y , b=false , sel=zy , out=ymux );
    Not16(in=ymux , out=notymux );
    Mux16(a=ymux , b=notymux , sel=ny , out=ymux2 );
    
   // adding, anding 
    And16(a=xmux2 , b=ymux2 , out=xyand );
    Add16(a=xmux2 , b=ymux2 , out=xyadd );
    
    //final check
    Mux16(a=xyand , b=xyadd , sel=f , out=outmux );
    Not16(in=outmux , out=notoutmux );
    
    // final output, checking if negative or not, and if zero
    Mux16(a=outmux , b=notoutmux , sel=no , out=out , out[15]=ng , out[0..7]=zr1 , out[8..15]=zr2);
    
    // checks each half of the out's if zero, then flipping value (zero = 1)
    Or8Way(in=zr1, out=or1);
    Or8Way(in=zr2, out=or2);
    Or(a=or1, b=or2, out=nonzero);
    Not(in=nonzero, out=zr);
}
